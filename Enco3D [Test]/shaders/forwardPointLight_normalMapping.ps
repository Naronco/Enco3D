#version 120

varying vec2 texCoord;
varying vec3 worldPos;
varying mat3 tbnMatrix;

struct PointLight
{
	vec4 color;
	float intensity;
	vec3 position;
	float range;
};

struct Material
{
	vec4 diffuseColor;
	sampler2D diffuseTexture;
	sampler2D normalTexture;
	float specularIntensity;
	float specularExponent;
};

uniform PointLight pointLight;
uniform Material material;

uniform vec3 eyePos;

void main()
{
	vec3 normal = tbnMatrix * normalize(texture2D(material.normalTexture, texCoord.xy).rgb * 2.0f - 1.0f);

	vec3 diffuse = vec3(0, 0, 0);
	vec3 specular = vec3(0, 0, 0);
	
	vec3 lightDirection = worldPos - pointLight.position;
	float distanceToLight = length(lightDirection);
	
	if(distanceToLight < pointLight.range)
	{
		lightDirection = normalize(lightDirection);
		float attenuation = 1.0f - (distanceToLight / pointLight.range);
		
		vec3 diffuseColor = texture2D(material.diffuseTexture, texCoord.xy).rgb * material.diffuseColor.rgb;
		float diffuseIntensity = max(dot(-lightDirection, normal), 0.0) * attenuation;
		diffuse = diffuseColor * diffuseIntensity * pointLight.color.rgb;
		
		if(diffuseIntensity > 0.0 && material.specularIntensity > 0.0)
		{
			vec3 reflectionDirection = normalize(reflect(lightDirection, normal));
			vec3 viewDirection = normalize(eyePos - worldPos);
			float specularIntensity = dot(viewDirection, reflectionDirection);
			specularIntensity = pow(specularIntensity, material.specularExponent);
			
			if(specularIntensity > 0.0)
			{
				specular = material.specularIntensity * specularIntensity * pointLight.color.rgb;
			}
		}
	}
	
	gl_FragColor = vec4(diffuse + specular, 1);
}